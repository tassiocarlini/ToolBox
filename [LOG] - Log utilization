select name, log_reuse_wait, log_reuse_wait_desc  from sys.databases -- where name = 'TAF_CRNB0K
go
DBCC sqlperf(logspace)
go


SELECT 
   GETDATE() AS [Current Time],
   [des].[login_name] AS [Login Name],
   DB_NAME ([dtdt].database_id) AS [Database Name],
   [dtdt].[database_transaction_begin_time] AS [Transaction Begin Time],
   [dtdt].[database_transaction_log_bytes_used] AS [Log Used Bytes],
   [dtdt].[database_transaction_log_bytes_reserved] AS [Log Reserved Bytes],
   des.session_id, -- Adicionado manualmente
   SUBSTRING([dest].text, [der].statement_start_offset/2 + 1,(CASE WHEN [der].statement_end_offset = -1 THEN LEN(CONVERT(nvarchar(max),[dest].text)) * 2 ELSE [der].statement_end_offset END - [der].statement_start_offset)/2) as [Query Text]
FROM 
   sys.dm_tran_database_transactions [dtdt]
   INNER JOIN sys.dm_tran_session_transactions [dtst] ON  [dtst].[transaction_id] = [dtdt].[transaction_id]
   INNER JOIN sys.dm_exec_sessions [des] ON  [des].[session_id] = [dtst].[session_id]
   INNER JOIN sys.dm_exec_connections [dec] ON   [dec].[session_id] = [dtst].[session_id]
   LEFT OUTER JOIN sys.dm_exec_requests [der] ON [der].[session_id] = [dtst].[session_id]
   CROSS APPLY sys.dm_exec_sql_text ([dec].[most_recent_sql_handle]) AS [dest]
GO





SET NOCOUNT ON;
 
IF OBJECT_ID(N'tempdb..#OpenTranStatus', N'U') IS NOT NULL
    DROP TABLE #OpenTranStatus;
GO
IF OBJECT_ID(N'tempdb..#OpenTranStatusStg', N'U') IS NOT NULL
    DROP TABLE #OpenTranStatusStg;
GO
 
-- Create the temporary table to accept the results.
CREATE TABLE #OpenTranStatus
    (
      -- dbname NVARCHAR(40),
      dbname VARCHAR(100) ,
      ActiveTransaction VARCHAR(25) ,
      Details SQL_VARIANT
    );
 
CREATE TABLE #OpenTranStatusStg
    (
      -- dbname NVARCHAR(40),
      ActiveTransaction VARCHAR(25) ,
      Details SQL_VARIANT
    );
 
DECLARE @dbid INT ,
    @dbname NVARCHAR(50) ,
    @SQLStmt NVARCHAR(1000);
 
DECLARE db_cursor CURSOR
FOR
    SELECT  database_id dbid ,
            name dbname
    FROM    sys.databases
    WHERE   state_desc = 'ONLINE'
    ORDER BY name;
 
OPEN db_cursor;
 
FETCH NEXT FROM db_cursor
INTO @dbid, @dbname;
 
WHILE @@FETCH_STATUS = 0
    BEGIN
        TRUNCATE TABLE #OpenTranStatusStg;
        SET @SQLStmt = 'DBCC OPENTRAN (' + CAST(@dbid AS NVARCHAR(100))
            + ') WITH TABLERESULTS, NO_INFOMSGS';
        INSERT  INTO #OpenTranStatusStg
                EXEC ( @SQLStmt
                    );
        INSERT  INTO #OpenTranStatus
                SELECT  @dbname ,
                        *
                FROM    #OpenTranStatusStg;
 
        -- Get the next vendor.
        FETCH NEXT FROM db_cursor
    INTO @dbid, @dbname;
    END;
CLOSE db_cursor;
DEALLOCATE db_cursor;
 
SELECT  *
FROM    #OpenTranStatus;
